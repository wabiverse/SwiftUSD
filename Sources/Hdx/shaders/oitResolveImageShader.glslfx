-- glslfx version 0.1

//
// Copyright 2018 Pixar
//
// Licensed under the terms set forth in the LICENSE.txt file available at
// https://openusd.org/license.
//

-- configuration
{
    "techniques": {
        "default": {
            "fragmentShader": {
                "source": [ "OitResolve.Image" ]
            }
        }
    }
}

--- --------------------------------------------------------------------------
-- layout OitResolve.Image

[
    ["buffer readWrite", "CounterBuffer", "hdxOitCounterBuffer",
        ["atomic_int", "hdxOitCounterBuffer"]
    ]
]

-- glsl OitResolve.Image

vec4 imageShader(vec2 uv)
{
#if defined(HD_HAS_hdxOitDataBuffer)
    const int screenWidth = int(HdGet_oitScreenSize().x);
    const int screenHeight = int(HdGet_oitScreenSize().y);

    // Must match the per-pixel sample count used when creating the OIT buffers.
    // (See HdxOitResolveTask::_PrepareOitBuffers)
    const int maxSamples = 8;

    const int dataBufferSize = screenWidth * screenHeight * maxSamples;

    // +1 because index 0 of counter buffer is reserved as atomic counter in
    // WriteOitLayersToBuffer
    int screenIndex = int(gl_FragCoord.x) + int(gl_FragCoord.y) * screenWidth;
    screenIndex += 1;

    int nodeIndex = ATOMIC_LOAD(hdxOitCounterBuffer[screenIndex]);
    int numDepths = 0;

    // XXX renderPass.WriteOitLayersToBuffer does not clamp the number of
    //     depth samples we store for a pixel. Here we process no more than
    //     'maxSamples' for a pixel. (If there are greater than 'maxSamples'
    //     samples stored for this pixel some will currently not contribute)
    vec4 sortedColor[maxSamples];
    float sortedDepth[maxSamples];

    while (nodeIndex != -1 && 
           numDepths < maxSamples && 
           nodeIndex < dataBufferSize) 
    {
        float currentDepth = hdxOitDepthBuffer[nodeIndex]; 
        int insertIndex = numDepths; 
        // Recall that depths are in eye space, so inequality is flipped.
        while (insertIndex > 0 && sortedDepth[insertIndex - 1] < currentDepth) {
            sortedDepth[insertIndex] = sortedDepth[insertIndex - 1];
            sortedColor[insertIndex] = sortedColor[insertIndex - 1]; 
            insertIndex -= 1; 
        }
        sortedColor[insertIndex] = hdxOitDataBuffer[nodeIndex];
        sortedDepth[insertIndex] = hdxOitDepthBuffer[nodeIndex];
        numDepths += 1; 
        nodeIndex = hdxOitIndexBuffer[nodeIndex]; 
    } 

    // Assume color in sortedColor is pre-multiplied by alpha
    int depth = 0;
    vec4 colorAccum = vec4(0);
    while (depth < numDepths) {
        colorAccum += sortedColor[depth] * (1 - colorAccum.a);

        if (colorAccum.a >= 1.0) break;

        depth += 1; 
    }

    colorAccum = clamp(colorAccum, vec4(0), vec4(1));
    return colorAccum;
#else
    return vec4(0);
#endif
}
